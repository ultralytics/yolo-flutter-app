group 'com.ultralytics.yolo'
version '1.0'

buildscript {
    // Align with example app (plugins { org.jetbrains.kotlin.android 2.1.0 })
    ext.kotlin_version = "2.1.0"
    repositories {
        google()
        mavenCentral()
        // For Flutter engine Maven artifacts
        maven { url 'https://storage.googleapis.com/download.flutter.io' }
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.6.0")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        // For Flutter engine Maven artifacts
        maven { url 'https://storage.googleapis.com/download.flutter.io' }
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

// Flutter SDK path
def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    // Try to get Flutter root from environment variable
    flutterRoot = System.getenv('FLUTTER_ROOT')
}

// Resolve Flutter engine version (used for embedding artifacts)
def flutterEngineVersion = null
if (flutterRoot != null) {
    def engineVersionFile = file("${flutterRoot}/bin/internal/engine.version")
    if (engineVersionFile.exists()) {
        flutterEngineVersion = engineVersionFile.text.trim()
    }
}

android {
    namespace = "com.ultralytics.yolo"
    compileSdk = 35

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString() // Use toString() for jvmTarget
    }

    // Configure Java Toolchain to use JDK 17
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    defaultConfig {
        minSdk = 21
        
        // Native library build configuration
        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_STL=c++_shared"
            }
        }

        // Supported ABIs
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            // NDK r28: 16KB page size support enabled by default
            ndkVersion = "28.0.13004108"
        }
    }
    
    // CMake build configuration
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    dependencies {
        // Use Flutter embedding from Maven coordinates (versioned by engine SHA)
        if (flutterEngineVersion != null) {
            compileOnly "io.flutter:flutter_embedding_debug:1.0.0-${flutterEngineVersion}"
            compileOnly "io.flutter:flutter_embedding_profile:1.0.0-${flutterEngineVersion}"
            compileOnly "io.flutter:flutter_embedding_release:1.0.0-${flutterEngineVersion}"
        } else {
            // Fallback for environments where the engine version can't be read.
            // The Flutter plugin loader (when invoked via `flutter build`) will supply versions.
            compileOnly 'io.flutter:flutter_embedding_debug'
            compileOnly 'io.flutter:flutter_embedding_profile'
            compileOnly 'io.flutter:flutter_embedding_release'
        }

        implementation("com.google.ai.edge.litert:litert:1.4.0")
        implementation("com.google.ai.edge.litert:litert-gpu:1.4.0")
        implementation('com.google.ai.edge.litert:litert-support-api:1.4.0')
        implementation('com.google.ai.edge.litert:litert-metadata:1.4.0')
        testImplementation("junit:junit:4.13.2")
        androidTestImplementation("androidx.test.ext:junit:1.1.5")
        androidTestImplementation("androidx.test.espresso:espresso-core:3.5.1")
        implementation("org.yaml:snakeyaml:1.29")
        implementation("androidx.camera:camera-core:1.2.3")
        implementation("androidx.camera:camera-camera2:1.2.3")
        implementation("androidx.camera:camera-lifecycle:1.2.3")
        implementation("androidx.camera:camera-view:1.2.3")
        implementation("androidx.core:core-ktx:1.10.1")
        implementation("androidx.appcompat:appcompat:1.6.1")
        
        testImplementation("org.jetbrains.kotlin:kotlin-test")
        testImplementation("org.mockito:mockito-core:5.0.0")
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }
}
